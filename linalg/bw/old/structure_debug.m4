divert(-1)

Copyright (C) 1999--2006 Emmanuel Thom'e

define(`print_row',``
void p_$1row(bw_$1mat mat, int i)
{
	int j;

	printf("[ ");
	for(j = 0 ; j < $3 ; j++) {
		$4($1mat_scal(mat,i,j));
		if (magma_display && j+1 < $3 )
			printf(",%c", (magma_display==2)?0x20:0x09);
		else
			printf(" ");
		/*
			*/
	}
	printf("]\n");
}'')


define(`print_col',``
void p_$1col(bw_$1mat mat, int j)
{
	int i;

	printf("\n");
	if (magma_display) {
		printf("[ ");
		for(i = 0 ; i < $2 ; i++) {
			$4($1mat_scal(mat,i,j));
			if (magma_display !=2 && i+1<$2)
				printf(",\n  ");
		}
		printf(" ]\n");
	} else {
		for(i = 0 ; i < $2 ; i++) {
			printf("[ ");
			$4($1mat_scal(mat,i,j));
			printf(" ]\n");
		}
	}
}'')


define(`print_mat',``
void p_$1mat(bw_$1mat mat)
{
	int i;
	if (magma_display) {
		int i,j;

		printf("[ ");
		for(i = 0 ; i < $2 ; i++) {
			for(j = 0 ; j < $3 ; j++) {
				$4($1mat_scal(mat,i,j));
				if ((i+1<$2 || j+1<$3) && magma_display) {
					printf(",%c",
						(magma_display==2)?0x20:0x09);
				}
			}
			if (magma_display !=2 && i+1<$2)
				printf("\n  ");
		}
		printf("    ]\n");
	} else {
		for(i = 0 ; i < $2 ; i++)
			p_$1row(mat,i);
	}
}'')

define(`print_poly',``
void p_$1poly(bw_$1poly x, int d)
{
	int i;
	printf("[\n");
	for(i = 0 ; i <= d ; i++) {
		p_$1mat($1poly_coeff(x,i));
		if (magma_display && i < d)
			printf(",\n");

	}
	printf("]\n");
}'')

define(`helpers_poly',``
bw_$1mat h_$1coeff(bw_$1poly p, int t)
{
	return $1poly_coeff(p,t);
}'')


define(`debug_k_matrix_type',`dnl
print_row($1,$2,$3,`k_print')
print_col($1,$2,$3,`k_print')
print_mat($1,$2,$3,`k_print')
print_poly($1)
helpers_poly($1)')

define(`debug_l_matrix_type',`dnl
print_row($1,$2,$3,`l_print')
print_col($1,$2,$3,`l_print')
print_mat($1,$2,$3,`l_print')
print_poly($1)
helpers_poly($1)')

divert(0)dnl
ifelse(outputfile,`DEBUG',`dnl
/* Copyright (C) 1999--2006 Emmanuel Thome */

/* This file was generated by m4 on esyscmd(echo -n $(date)) */
/* Do not edit this file. Re-generate instead from the source (using m4) */

#ifndef INCLUDING_STRUCTURE_DEBUG_AUTOMATIC_H_
#error "Do not include this file directly\n"
#endif

#ifndef STRUCTURE_DEBUG_AUTOMATIC_H_
#define STRUCTURE_DEBUG_AUTOMATIC_H_

#ifdef __cplusplus
extern "C" {
#endif
debug_k_matrix_type(`mn',`m_param',`n_param')
debug_k_matrix_type(`nb',`n_param',`bigdim')
debug_k_matrix_type(`mb',`m_param',`bigdim')
debug_k_matrix_type(`bb',`bigdim',`bigdim')

debug_l_matrix_type(`x_mn',`m_param',`n_param')
debug_l_matrix_type(`x_nb',`n_param',`bigdim')
debug_l_matrix_type(`x_mb',`m_param',`bigdim')
debug_l_matrix_type(`x_bb',`bigdim',`bigdim')

#ifdef __cplusplus
}
#endif

#endif
')dnl
